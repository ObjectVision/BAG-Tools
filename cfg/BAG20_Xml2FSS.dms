////////////////////////////////////////////////////////////////////////////////////////
//                                                                                    //
//                                                                                    //
//              Configuratie voor project: BAG 2.0 XML bronfiles > FSS                //
//                                                                                    //
//                              Object Vision 2023                                    //
//                                                                                    //
////////////////////////////////////////////////////////////////////////////////////////
//                                                                                    //
// Hoe een nieuwe BAG te verkrijgen en tot FSS Files om te zetten:                    //
// 1. Download je  BAG via:                                                           //
// https://www.kadaster.nl/-/kosteloze-download-bag-2.0-extract                       //
//                                                                                    //
// 2. Pak alleen de zip bestanden:                                                    //
// - 9999LIGdatum                                                                     //
// - 9999NUMdatum                                                                     //
// - 9999OPRdatum                                                                     //
// - 9999PNDdatum                                                                     //
// - 9999STAdatum                                                                     //
// - 9999VBOdatum                                                                     //
// - 9999WPLdatum                                                                     //
// uit in een subfolder en configureer deze als XmlDir                                //
//                                                                                    //
// 3. Configureer de date parameter met als conventie: yyyymmdd                       //
//                                                                                    //
// 4. Open deze configuratie en activeer de optie: MaakFSSBestanden                   //
//                                                                                    //
//  De BAG inclusief historie wordt nu weggeschreven naar een set van FSS Files in    //
//  de FSSDir.                                                                        //
//                                                                                    //
// Voor meer informatie, zie:                                                         //
//  https://github.com/ObjectVision/BAG-Tools/wiki/BAG20-Xml2FSS                      //
//                                                                                    //
////////////////////////////////////////////////////////////////////////////////////////

container BAG20_Xml2FSS: IntegrityCheck = "GeoDMSVersion() >= 8.08005" // GeoDMS version should be 8.81 or later
{
	// te configureren
	parameter<string> date     := '20230411';
	//

	parameter<string> date_dir := '%SourceDataDir%/BAG20/' + date;

	parameter<string> XmlDir   := date_dir + '/NGR';
	parameter<string> FSSDir   := date_dir + '/fss.new';

	parameter<string> MaakFSSBestanden := xml/folderinfo/CanGenerate == 2b
		? 'Cannot make list of files, ' + expand(., XmlDir) + ' does not consist or contains no .xml files.'
		:  xml/folderinfo/CanGenerate == 1b
			? 'Data from ' + expand(., XmlDir) + ' is probably not in requested BAG 2.0 format, make sure you only unzipped: LIG, NUM, OPR, PND, STA, VBO, WPL ZIP files'
			: 'Ready'
	, ExplicitSuppliers = "= SupplierList";

	parameter<string> SupplierList     := xml/folderinfo/CanGenerate == 0b
		? 'FSS/ligplaats;FSS/standplaats;FSS/pand;FSS/vbo;FSS/nummeraanduiding;FSS/openbareruimte;FSS/woonplaats;FSS/overzicht'
		: '';

	unit<dpoint> rdc_base;
	unit<dpoint> rdc_meter := range(rdc_base, point(300000d,0d), point(625000d,280000d));
	unit<ipoint> rdc_mm    := gridset(rdc_meter, point(0.001, 0.001), point(0.0,0.0), ipoint);

	parameter<uint32> srsDimensionDefaultValue := 2;

	container typeringen
	{
		unit<uint8> gebruiksdoel_vbo : nrofrows = 11
		{
			attribute<string> label:
			[
			  'bijeenkomstfunctie','celfunctie','gezondheidszorgfunctie','industriefunctie','kantoorfunctie','logiesfunctie'
			 ,'onderwijsfunctie','overige gebruiksfunctie','sportfunctie','winkelfunctie','woonfunctie'
			];
			attribute<string> name      := replace(label,' ','_');
			attribute<string> name_kort := replace(name, 'functie','');
		}
	}

	#include<FSS.dms>
	#include<XML.dms>
	#include<Templates.dms>
	#include<overzichten.dms>
}
